# Chat Mentions by Gloozzy
#
# How do I use it?
#   Say a player in the server's name and it will mention them if they have chat mentions on.
#
# How do I disable chat mentions?
#   Execute the command /options and click the red dye (chat mentions toggle button)

on join:
	if {options::%uuid of player%::mentions} is not set:
		set {options::%uuid of player%::mentions} to true

command /options:
	aliases: /controls, /profile
	trigger:
		options(player)
			
function options(p: player):
	create a gui with virtual chest inventory with size 5 named "User Profile":
		make gui slot 13 with skull of {_p} named "&b%{_p}%" with lore " ", "&bLives&f: &7%{hearts::%uuid of {_p}%}%", "&bKills: &7%{kills.%uuid of {_p}%}%", "&bDeaths: &7%{deaths.%uuid of {_p}%}%"
		if {options::%uuid of {_p}%::mentions} is true:
			make gui slot 31 with lime dye named "&bChat Mentions" with lore " ", "&aOn":
				set {options::%uuid of {_p}%::mentions} to false
				options({_p})
		else:
			make gui slot 31 with red dye named "&bChat Mentions" with lore " ", "&cOff":
				set {options::%uuid of {_p}%::mentions} to true
				options({_p})
		make gui slot 44 with end portal frame named "&bCredits" with lore " ", "&8- &bSkript made by &b&lGloozzy"
		open last created gui to {_p}

on chat:
	loop all players:
		if message contains "%loop-player%":
			if {options::%uuid of loop-player%::mentions} is true:
				replace all "%loop-player%" with "&e@%loop-player%&r" in message
				play sound "minecraft:entity.experience_orb.pickup" with volume 1 and pitch 2 to loop-player
